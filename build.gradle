group 'mumuksia'
version '1.0-SNAPSHOT'

buildscript {
    ext.kotlin_version = '1.2.31'
    ext.spring_boot_version = '1.5.4.RELEASE'

    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-allopen:$kotlin_version"
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$spring_boot_version"
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.0'
        classpath('se.transmode.gradle:gradle-docker:1.2')
    }
}

apply plugin: 'kotlin'
apply plugin: "kotlin-spring"
apply plugin: 'org.springframework.boot'
apply plugin: 'org.junit.platform.gradle.plugin'
apply plugin: 'docker'

repositories {
    mavenCentral()
}

jar{
    baseName = 'personalizer'
    version = '0.0.1'
}

task buildDocker(type: Docker, dependsOn: build) {
    push = true
    applicationName = jar.baseName
    dockerfile = file('src/main/kotlin/docker/DockerFile')
    doFirst {
        copy {
            from jar
            into stageDir
        }
    }
}

junitPlatform {
    filters {
        engines {
            include 'spek'
        }
    }
}

dependencies {
    //compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version" // Required for Kotlin integration
    compile 'org.springframework.boot:spring-boot-starter-web'
    compile 'org.jsoup:jsoup:1.12.1'
    compile group: 'javax.mail', name: 'mail', version: '1.4.7'
    compile("org.springframework.boot:spring-boot-starter-mail")
    testCompile('org.springframework.boot:spring-boot-starter-test')
    testCompile 'org.jetbrains.spek:spek-api:1.1.5'
    testCompile 'org.junit.platform:junit-platform-runner:1.0.0'
    testRuntime 'org.jetbrains.spek:spek-junit-platform-engine:1.1.5'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
